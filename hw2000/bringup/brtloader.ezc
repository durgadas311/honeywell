00010* LOADER FOR MAG TAPE USING BRT FORMAT
00020         PROG  BRTLDR
00040         ADMODE3
00450*
00450* TODO: LAYOUT COMMUNICATIONS AREA
00030         ORG   0
00060         RESV  1
00070  IX1    RESV  4     INDEX REGISTER X1, ETC
00080  IX2    RESV  4
00090  IX3    RESV  4
00100  IX4    RESV  4
00110  IX5    RESV  4     DIST PTR
00120  IX6    RESV  4     REC PTR
00130  IX7    RESV  4
00140  IX8    RESV  4
00150  IX9    RESV  4
00160  IX10   RESV  4
00170  IX11   RESV  4
00180  IX12   RESV  4
00190  IX13   RESV  4
00200  IX14   RESV  4
00210  IX15   RESV  4
00210         RESV  3    61..63
00210  METH   RESV  1    64 CONSOLE CALL METHOD
00210  REV    RESV  3    65..67 CURRENT LOADED REV
00210  NAME   RESV  6    68..73 NAME TO LOAD
00210  SEG    RESV  2    74..75 SEG TO LOAD
00210  TPU    RESV  1    76 TAPE DRIVE UNIT
00210  HLTNM  RESV  8    77..84 HALT NAME
00210  CCID   RESV  1    85 CALL CARD ID
00210  FXST0  RESV  4    86..89 FIXED START 0
00210  FXST1  RESV  4    90..93 FIXED START 1
00210  FXST2  RESV  4    94..97 FIXED START 2
00210  FXST3  RESV  4    98..101 FIXED START 3
00210  OCEXT  RESV  4    102..105 OWN-CODE EXIT
00210  SDIR   RESV  1    106 SEARCH DIRECTION
00210  RELAV  RESV  3    107..109 RELOC AUGMENT
00210  RELP   RESV  1    110 RELATIVE POSITION
00210  SERM   RESV  1    111 SEARCH MODE
00210  STRM   RESV  1    112 START MODE
00210  VISIB  RESV  6    113..118 VISIBILITY MASK
00210  SPSTV  RESV  3    119..121 SPECIAL START LOC
00210  RETO1  RESV  4    122..125 OWN-CODE RET 1
00210  RETO2  RESV  4    126..129 OWN-CODE RET 2
00210  RETN   RESV  9    130..138 RETURN FOR NORMAL CALL
00210  GRETV  RESV  3    139..141 GENERAL RETURN ADR
00210  DATE   RESV  5    142..146 CURRENT DATE
00210  TRAPM  RESV  1    147 TRAPPING MODE
00210  ARET   RESV  3    148..150 ALT RET ADR (READ NEXT CARD)
00210  ECDF   RESV  4    151..154 ECD FIELD
00210  TYWRA  RESV  1    155 TYPEWRITER AVAILABILITY
00450* REST IS GLUE CODE, 156..1339
00450*
00030         ORG   20000     TODO: NEED REAL ADDRESS
00040         ADMODE4
00060* SW INSTRUCTIONS HERE, TO REPAIR PUNC LOST ON TAPE
00060* ONCE WE LOAD WE NEVER NEED THIS AGAIN
00060*
00420  BOOT   NOP          SEARCH/LOAD/GO AAAMONS1
00060*
00050*        MUST ENSURE NO PUNC IN HEADER BUFFER
00430         LCA   HPTR,STRT
00430         LCA   HPTRE,ENDC
00050         EXM   FZERO,FILL,07     CLEARS PUNC
00430         B     CLEER
00430         B     INIT
00060* SET TARGET PROGRAM NAME "AAAMONS1"
00430         B     SEARCH
00430         BCE   0+X5,STRM,45      START MODE N?
00430         H     *
00060*
00060* THIS SHOULD ONLY BE DONE WHEN COLD-START,
00060* NOT DURING PROGRAM CHAINING
00420  INIT   SCR   INITX,70              SET RET ADR
00430         EXM   TMPLT,METH,77         MOVE TIL RM
00430   INITX B     0                     RETURN TO CALLER
00060*
00060* SETUP COMMUNICATIONS AREA, HALT (3?)
00060*
00060* TODO: EITHER TAPE OR CARD SEARCH/LOAD, "AAAMONS1"
00060*
00060*
00420  SEARCH SCR   SEARCX,70         SET RET ADR
00420  AGAIN  PDT   HEADER,11,40,60   LOAD SEG HEADER DATA
00430         PCB   *,11,40,00
00430         SCR   CLC,01            MUST MATCH RWC
00430         SCR   SLC,11            MUST MATCH RWC
00430         BS    SLC,CLC
00430         BCT   DONE,60           ZERO = EOF - SHOULD NEVER GET
00430         C     HDR1,HEADER+4
00430         BCT   AGAIN,42          1HDR = SKIP
00430         C     TRL1,HEADER+4
00430         BCT   DONE,42           1END = REVERSE - FOR NOW STOP
00430         C     ERI1,HEADER+4
00430         BCT   DONE,42           1ERI = PAST END - FOR NOW STOP
00430         BCE   GOTSEG,HEADER,50  SEGMENT HEADER
00430         BCE   GOTSEG,HEADER,54  SEGMENT HEADER (NO RECS)
00430         B     AGAIN             SKIP NON-SEG RECS HERE (ERROR?)
00060* TODO: SEARCH FOR SEG HDR MATCHING NAME
00430  GOTSEG NOP
00430         LCA   HPTR,IX6           STARTING PTR
00430         MCW   17+X6,SEG          MOVE NAME+SEG
00060         B     TREC               TODO: CALL TREC OR CREC
00430   SEARCXB     0                  RETURN TO CALLER
00060  DONE   H     *,7682             OCTAL 17002, HALT-3
00060         B     AGAIN
00060*
00060* LOADS NEXT UNIT (UNTIL 61) THEN HALTS
00060* DECODE 42 (BOOTSTRAP), 50/54 (SEGMENT), 41/44 (NON-SEG)
00060* CURRENTLY DOESN'T CHECK BANNER CHAR...
00060* TODO: EXPAND INTO FULL SEARCH FOR NAMED PROGRAM
00060* ALSO SUPPORT RELOC AND RANGE
00060*
00060* LOAD SEGMENT FROM TAPE
00060* CALLER ALREADY LOADED FIRST RECORD...
00060* TODO: ALLOW SELECTION OF DRIVE UNIT
00420  TREC   SCR   TRECX,70              SET RET ADR
00420         LCA   ZEROA,IX5             INIT DIST PTR
00430         B     TREC1
00420  TREC0  PDT   HEADER,11,40,60       LOAD HEADER DATA
00430         PCB   *,11,40,00
00060*        CHECK ERRORS? EOT? EOF? SEARCH?
00430  TREC1  B     LOAD
00430         BCT   TREC0,60              LOOP IF ZERO-BALANCE
00430   TRECX B     0                     RETURN TO CALLER
00060*
00060* LOAD SEGMENT FROM CARDS
00060* CALLER ALREADY LOADED FIRST CARD...
00420  CREC   SCR   CRECX,70              SET RET ADR
00420         LCA   ZEROA,IX5             INIT DIST PTR
00430         B     CREC1
00420  CREC0  PDT   HEADER,12,41          LOAD HEADER DATA
00430         PCB   *,12,41,10
00430         PCB   CREC9,12,41,41        END OF DECK (ERROR)
00430  CREC1  MCW   EIGHTY,HEADER+3
00430         B     LOAD
00430         BCT   CREC0,60              LOOP IF ZERO-BALANCE
00430  CREC9  NOP
00430   CRECX B     0                     RETURN TO CALLER
00060*
00060* LOAD PROGRAM DATA FROM HEADER
00060* RETURNS ZERO-BALANCE IF NOT LAST RECORD
00060* WORKS FOR CARD AND TAPE RECORDS?
00430  LOAD   SCR   LOADX,70           SET RETURN ADDRESS
00430         LCA   HPTR,IX6           STARTING PTR
00430         MCW   0+X6,BANR          GET BANNER CHAR
00430         LCA   HPTR,REND          COMPUTE END OF REC
00430         BA    3+X6,REND          REC LEN (NOT CARDS?)
00430         MCW   6+X6,CLEN          GET REC CTL LEN
00060*        ANY MORE DATA FROM HDR?
00430         BA    CLEN,IX6           PTR TO PROG DATA
00060* PARSE NEXT BRT COMMAND
00430  NEXT   BCC   CTLC,0+X6,03       CHECK FOR 11XXXX
00060*        4 LSB BITS HAVE STRING LEN
00430         SST   0+X6,SLEN,17       LEN OF STRING
00430* MUST NOT SET PUNC UNTIL AFTER MOVE (DESTROYS PUNC)
00430         LCA   IX6,ENDC
00430         BA    SLEN,ENDC
00430         BA    ONE,ENDC           POINTS ONE PAST LAST
00430         SW    (ENDC-3)           MARK END OF MOVE
00430         EXM   1+X6,0+X5,37       INCL ONE EXTRA+WM
00430         CW    (ENDC-3)           MARK END OF MOVE
00430         BCC   STW1,0+X6,01       NEEDS WM
00430         BCC   STI1,0+X6,02       NEEDS IM
00430  MOVE   BA    SLEN,IX5
00430         CW    0+X5               CLEANUP STRAY WM
00430         BA    SLEN,IX6
00430  NEXTE  BA    ONE,IX6
00430  NEXTF  C     IX6,REND
00430         BCT   NEXT,44            CONT IF IX6 < REND
00430  NEXTR  EXT   EMSK,BANR          ZERO IF NOT LAST
00430         BA    BANR,BANR          GET ZERO-BALANCE STATUS
00430   LOADX B     0                  ZERO IF MORE RECORDS
00060*
00450* EXIT IS VIA CTLC ON CASE OF 61 CHAR
00450* NEXT REC IS VIA CTLC ON CASE OF 77 CHAR
00450*
00430  STW1   SW    0+X5
00430         B     MOVE
00450*
00430  STI1   SI    0+X5
00430         B     MOVE
00450*
00430  CTLC   BCE   LDST,0+X6,60       SET DIST PTR
00430         BCE   TERM,0+X6,61       TERM LOAD
00430         BCE   CLER,0+X6,62       CLEAR AREA
00430         BCE   SETW,0+X6,63       SET WORD MARK
00430         BCE   SETI,0+X6,64       SET ITEM MARK
00450* WE SHOULD ONLY GET 77 IF BANNER IS 50,41
00430         BCE   NEXTR,0+X6,77      END OF RECORD
00450*        ERROR...
00450         H     *
00450*
00430  LDST   MCW   3+X6,IX5
00430         BA    FOUR,IX6
00430         B     NEXTF
00450*
00430  CLER   MCW   3+X6,STRT
00050         MCW   6+X6,ENDC
00050         EXM   7+X6,FILL,07     CLEARS PUNC
00430         BA    EIGHT,IX6
00430         B     CLEER
00430         B     NEXTF
00450*
00430  SETW   SW    0-1+X5
00430         B     NEXTE
00450*
00430  SETI   SI    0-1+X5
00430         B     NEXTE
00450*
00450* TODO: CAN LOAD RESUME? IS THIS GUARANTEED LAST REC?
00430  TERM   MCW   3+X6,IX5        SET GO ADR
00430         SST   NEG1,BANR,04    FORCE END-OF-LOAD COND
00430         B     NEXTR           TODO: ALSO INDICATE GOTO
00450*
00450* CLEAR/FILL MEMORY - MUST CLEAR PUNC TOO
00450* CALLER SETS STRT, ENDC, FILL (W/PUNC)
00430  CLEER  SCR   CLERX,70         SET RETURN ADDRESS
00050  CLER1  EXM   FILL,(STRT-3),07
00430         C     STRT,ENDC
00430         BA    ONE,STRT
00430         BCT   CLER1,44         CONT IF STRT < ENDC
00430   CLERX B     0
00450*
00440  NEG1   DCW   #1B-1
00440  ONE    DCW   #1B1
00440  FOUR   DCW   #1B4
00440  EIGHT  DCW   #1B8
00440  EIGHTY DCW   #3B80
00440  EMSK   DCW   #1C04     MASK FOR LAST RECORD
00440  HPTR   DSA   HEADER
00440  HPTRE  DSA   HEADER+256
00450*
00450  HDR1   DCW   @1HDR @      FIRST REC (LABEL)
00450  TRL1   DCW   @1EOF @      END REC (LABEL)
00450  ERI1   DCW   @1ERI @      PAST END REC (LABEL) (2X)
00450  MON1   DCW   @AAAMONS1@   STANDARD NAME OF MONITOR
00450*
00450  CLC    DSA   0
00450  SLC    DSA   0
00450*
00450  FZERO  DC    #1B0
00450  BANR   DCW   #1B0      BANNER CHAR
00450  SLEN   DCW   #1B0      STRING LEN
00450  CLEN   DCW   #1B0      REC CTL LEN (HDR)
00450  REND   DSA   0         RECORD PTR
00450  ZEROA  DCW   #3B0      INIT FOR DIST - MUST BE 3 CHAR
00450  STRT   DSA   0         CLEAR START ADDR
00450  ENDC   DSA   0         CLEAR END ADDR
00450  FILL   DC    #1B0      CLEAR FILL CHAR
00450*
00450* TEMPLATE FOR COMM AREA, 64..1339
00040         ADMODE3
00450  TMPLT  DCW   #1B0        CONS CALL METH
00450         DCW   @   @       REV
00450         DCW   @        @  PGM NAME + SEG
00450         DCW   #1B0        TAPE DRIVE UNIT
00450         DCW   @        @  HALT NAME
00450         DCW   #1B0        CALL CARD ID (COL 18)
00450         B     0           FIXED START 0
00450         B     0           FIXED START 1
00450         B     0           FIXED START 2
00450         B     0           FIXED START 3 (WM MUST FOLLOW)
00450         B     0           EXIT TO OWN-CODE (TBD)
00450         DCW   #1C22       SEARCH DIRECTION
00450         DSA   0           RELOC AUGMENT
00450         DCW   #1C01       REL POS
00450         DCW   #1C20       SEARCH MODE
00450         DCW   @N@         START MODE
00450         DCW   #6C400000000000  VISIBILITY MASK
00450         DSA   0           SPECIAL START LOC
00450         B     0           OWN-CODE RET 1
00450         B     0           OWN-CODE RET 2
00450         SCR   ERRX-TMPLT+64,70     RET FOR NORMAL CALL (TBD)
00450         B     RES2-TMPLT+64
00450         DSA   RES1-TMPLT+64
00450         DCW   @     @     CURR DATE (DDDYY?)
00450         DCW   #1B0        TRAPPING MODE
00450         DSA   130         ALT RET ADR (TBD)
00450         DCW   @JJ0#@      ECD
00450         DC    #1B0        CONS TYPWR AVAIL (WM/IM)
00450* END OF ADMODE3 COMM AREA... TBD ADMODE4 COMM AREA
00450  ERRX   DSA   0
00040         ADMODE4     -----------------------------------
00450  RES1   CAM   60
00450         B     BOOT    TBD IRES1
00450  RES2   CAM   60
00450         B     BOOT    TBD IRES2
00450 F       DC    #1B0
00450*
00450  HEADER EQU   *
00450* CLEAR WON'T WORK FOR BOOTSTRAP?
00450******   CLEAR HEADER,HEADER+256,Z
00720*
00720* EXCEPT WE DON'T WANT WM?
00720*  HEADERDA    1x256,X5
00720* BANNER       1
00720* LENGTH       2,4
00720* SEQUNC       5,6
00720* CTLLEN       7
00720* REVNUM       8,10      X DATA STARTS HERE FOR TYPE 41,44
00720* PROGNM       11,16     X
00720* SEGNAM       17,18     X
00720* VISIBL       19,24     X
00720* X DATA STARTS HERE FOR TYPE 50,54
00720*
00730         END   BOOT
