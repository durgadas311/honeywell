00010* LOADER FOR MAG TAPE USING BRT FORMAT
00020         PROG  BRTLDR
00040         ADMODE3
00450*
00030         ORG   0     TODO: NEED REAL ADDRESS
00060* SW INSTRUCTIONS HERE, TO REPAIR PUNC LOST ON TAPE
00060* ONCE WE LOAD WE NEVER NEED THIS AGAIN
00060*
00060* LOADS NEXT UNIT (UNTIL 61) THEN HALTS
00060* DECODE 42 (BOOTSTRAP), 50/54 (SEGMENT), 41/44 (NON-SEG)
00060* CURRENTLY DOESN'T CHECK BANNER CHAR...
00060* TODO: EXPAND INTO FULL SEARCH FOR NAMED PROGRAM
00060*
00420  BEGIN  PDT   HEADER,11,40,60       LOAD HEADER DATA
00430         PCB   *,11,40,00
00060*        CHECK ERRORS? EOT? EOF?
00430         MCW   HEADER+6,CLEN      GET REC CTL LEN
00430         LCA   HPTR,PTR           STARTING PTR
00430         BA    CLEN,PTR           PTR TO PROG DATA
00430  NEXT   NOP                      DEBUG: CHANGE TO H
00430         BCC   CTLC,(PTR-2),03    CHECK FOR 11XXXX
00430         MCW   (PTR-2),SLEN       LEN OF STRING
00430         EXT   SMSK,SLEN
00430         BCC   STW1,(PTR-2),01    NEEDS WM
00430         BCC   STI1,(PTR-2),02    NEEDS IM
00430  MOVE   BA    ONE,PTR
00430         LCA   PTR,ENDC
00430         BA    SLEN,ENDC
00430         SW    (ENDC-2)      MARK END OF MOVE
00430         EXM   (PTR-2),(DIST-2),11
00430         BA    SLEN,DIST
00430         BA    SLEN,PTR
00430         B     NEXT
00450* EXIT IS VIA CTLC ON CASE OF 61 CHAR
00450* NEXT REC IS VIA CTLC ON CASE OF 77 CHAR
00450*
00430  STW1   SW    (DIST-2)
00430         B     MOVE
00450*
00430  STI1   SI    (DIST-2)
00430         B     MOVE
00450*
00430  CTLC   BCE   LDST,(PTR-2),60       SET DIST PTR
00430         BCE   EXIT,(PTR-2),61       TERM LOAD
00430         BCE   CLER,(PTR-2),62       CLEAR AREA
00430         BCE   SETW,(PTR-2),63       SET WORD MARK
00430         BCE   SETI,(PTR-2),64       SET ITEM MARK
00430         BCE   BEGIN,(PTR-2),77      END OF RECORD
00450*        ERROR...
00450         H     *
00450*
00430  LDST   BA    THREE,PTR
00050         MCW   (PTR-2),DIST
00430         BA    ONE,PTR
00430         B     NEXT
00450*
00430  CLER   BA    THREE,PTR
00050         MCW   (PTR-2),STRT
00050         MCW   (PTR-2),STRT1
00430         BA    THREE,PTR
00050         MCW   (PTR-2),ENDC
00430         BA    ONE,PTR
00050         MCW   (PTR-2),FILL
00430         BA    ONE,PTR
00450* CLEAR MEMORY - ASSUMES NO PUNC EXISTS
00430         SW    (ENDC-2)      MARK END OF FILL
00050         MCW   FILL,(STRT-2)
00430         BA    ONE,STRT1
00430         EXM   (STRT-2),(STRT1-2),37    LEFT-RIGHT TO A WM
00430         CW    (ENDC-2)      ERASE OUR TRACKS
00430         B     NEXT
00450*
00430  SETW   BA    ONE,PTR
00430         BS    ONE,DIST      MORE ELEGANT WAY?
00430         SW    (DIST-2)
00430         BA    ONE,DIST
00430         B     NEXT
00450*
00430  SETI   BA    ONE,PTR
00430         BS    ONE,DIST      MORE ELEGANT WAY?
00430         SI    (DIST-2)
00430         BA    ONE,DIST
00430         B     NEXT
00450*
00430  EXIT   BA    THREE,PTR
00050         MCW   (PTR-2),DIST
00450* TODO: HALT HERE? OR JUST RUN?
00430         H     (DIST-2)
00450*
00440  ONE    DCW   #1B1
00440  THREE  DCW   #1B3
00440  SMSK   DCW   #1C17
00440  HPTR   DSA   HEADER
00450*
00450  SLEN   DCW   #1B0      STRING LEN
00450  CLEN   DCW   #1B0      REC CTL LEN (HDR)
00450  PTR    DSA   0         RECORD PTR
00450  DIST   DSA   0         DISTRIBUTION PTR
00450  STRT   DSA   0         CLEAR START ADDR
00450  STRT1  DSA   0
00450  ENDC   DSA   0         CLEAR END ADDR
00450  FILL   DCW   #1B0      CLEAR FILL CHAR
00450*
00450  HEADER EQU   *
00720*
00730         END   0
