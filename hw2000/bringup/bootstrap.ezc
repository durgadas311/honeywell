00010* BOOTSTRAP LOADER FOR MAG TAPE
00010* THIS PROGRAM SHOULD BE THE FIRST RECORD ON THE TAPE
00020         PROG  BOOTMT
00040         ADMODE3
00450*
00450* AFTER BOOTSTRAP, AAR/BAR POINT TO (THIS) LOAD ADDR,
00450* BUT CAN WE USE IT? WE HAVE NO WORD MARKS.
00450*
00030         ORG   0
00050         SW    BEGIN,BEGIN+7
00050         SW    BEGIN+14,BEGIN+35
00050         SW    BEGIN+42,BEGIN+49
00050         SW    MORE,MORE+8
00050         SW    NOWM,NOWM+8
00050         SW    NOIM,NOIM+7
00050         SW    NOIM+14,NOIM+21
00050         SW    NOIM+26,DONE
00050         SW    DONE+4,ONE
00060* FIXUP DATA WM AFTER LOADING HEADER
00060*
00060* SIMPLEST OBJECT FORMAT:
00060* PROG (NO PUNC) FOLLOWED BY PUNC DATA
00060* (IMAGE SIZE = 2 X PROG SIZE)
00060*
00420  BEGIN  PDT   LOAD-2,11,40,60       LOAD HEADER DATA
00430         PCB   *,11,40,00
00060*        CHECK ERRORS?
00060* TAPE LOAD DESTROYED WM SO REPLACE
00050         SW    LOAD-2,LENGTH-2
00050         SW    START-2,PUNC-2
00420         PDT   (LOAD-2),11,40,60     LOAD PROG CHARS
00430         PCB   *,11,40,00
00420         PDT   (PUNC-2),11,40,60     LOAD PROG PUNC
00430         PCB   *,11,40,00
00430  MORE   BBE   NOWM,(PUNC-2),01      1 MEANS NO WM
00050         SW    (LOAD-2)
00430  NOWM   BBE   NOIM,(PUNC-2),02      1 MEANS NO IM
00050         SI    (LOAD-2)
00050  NOIM   BA    ONE,LOAD
00050         BA    ONE,PUNC
00050         BS    ONE,LENGTH
00050         BCT   DONE,60
00050         B     MORE
00440  DONE   B     (START-2)
00450*
00440  ONE    DCW   #1B1
00450*
00450* NEXT RECORD ON TAPE MUST CONTAIN THIS DATA
00450* PREAMBLE CODE ALREADY (RE)PLACED WORD MARKS,
00450* FOLLOWED BY PROGRAM CHARS, FOLLOWED BY PUNC
00450* (THREE TAPE RECORDS).
00450* TODO: ALLOW LONGER PROGS BY USING TAPE BLOCKS
00450* NOT RECORDS
00450*
00460  LOAD   DSA   0
00460  LENGTH DSA   0
00460  START  DSA   0
00460  PUNC   DSA   0
00720*
00730         END   0
