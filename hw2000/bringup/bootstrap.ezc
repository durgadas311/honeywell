00010* BOOTSTRAP LOADER FOR MAG TAPE
00010* THIS PROGRAM SHOULD BE THE FIRST RECORD ON THE TAPE
00010* (OR SECOND, AFTER "1HDR ")
00020         PROG  BOOTMT
00040         ADMODE3
00450*
00030         ORG   0
00060* SW INSTRUCTIONS HERE, TO REPAIR PUNC LOST ON TAPE
00001         SW    CLER1+0,CLER1+8
00002         SW    CLER1+15,CLER1+22
00003         SW    CLER1+27,CLER1+29
00004         SW    MORE+0,MORE+8
00005         SW    MORE+16,MORE+22
00006         SW    MORE+28,MORE+37
00007         SW    MORE+43,ERROR+0
00008         SW    LOAD+0,LOAD+9
00009         SW    LOAD+18,LOAD+27
00010         SW    LOAD+36,LOAD+41
00011         SW    LOAD+51,LOAD+56
00012         SW    LOAD+65,LOAD+74
00013         SW    LOAD+83,LOAD+89
00014         SW    LOAD+94,CLC+1
00015         SW    SLC+1,HPTR+1
00016         SW    ONE+0,ONE+1
00017         SW    PTR+1,DIST+1
00018         SW    NREC+0,*+7
00060*
00060* MUST ENSURE BUFFER HAS NO STRAY PUNCTUATION
00430         LCA   HPTR,PTR
00050  CLER1  EXM   FILL,(PTR-2),07
00430         BA    ONE,PTR
00050         C     PTR,EPTR
00430         BCT   CLER1,44         CONT IF PTR < EPTR
00060*
00060* WE MUST USE ADR MODE 4 SINCE LOADER GOES IN HIGH MEM,
00060* BUT THAT CONTRIBUTES TO AN OVERSIZED BOOTSTRAP
00060*
00040         ADMODE4
00420         CAM   60
00420         LCA   RUN,DIST
00420  MORE   PDT   BUFFER,11,40,60       LOAD RECORD
00430         PCB   *,11,40,00
00430         SCR   CLC,01            MUST MATCH RWC
00430         SCR   SLC,11            MUST MATCH RWC
00430         BS    SLC,CLC
00430         C     ZERO,CLC
00430         BCT   ERROR,42          ERROR IF EOF
00430         BCE   LOAD,BUFFER,42    BOOTSTRAP BANNER CHAR
00430  ERROR  H     *                 CAN'T CONTINUE
00060*
00060* DON'T DEPEND ON PUNCTUATION IN BUFFER
00430  LOAD   LCA   HPTR,PTR
00430         LCA   HPTR,EPTR
00430         BA    CLC,EPTR          END OF RECORD
00430         BA    ONE,PTR           SKIP OVER BANNER CHAR
00060*        SINCE WE CLEARED ALL PUNC, ONLY NEED WM HERE
00430         SW    (EPTR-3)          SET WM
00430         EXM   (PTR-3),(DIST-3),37
00430         CW    (EPTR-3)          REMOVE WM IN CASE NEXT DIFF
00430         BA    CLC,DIST
00430         BS    ONE,DIST
00430         BS    ONE,NREC
00430         C     ZERO,NREC
00430         BCT   (RUN-3),42        RUN LOADER IF DONE
00430         B     MORE
00060*
00450  CLC    DSA   0
00450  SLC    DSA   0
00440  HPTR   DSA   BUFFER
00440  EPTR   DSA   BUFFER+256       RE-USED LATER
00450  FILL   DC    #1B0             NO PUNC
00450*
00450  ZERO   DCW   #1B0
00450  ONE    DCW   #1B1
00450  PTR    DSA   0         RECORD PTR
00450  DIST   DSA   0         DISTRIBUTION PTR (LOAD ADR)
00450* THESE VALUES ARE MANUALLY COPIED FROM BRT LOADER ASSEMBLY
00450  RUN    DSA   20000     LOAD/RUN ADR FOR LOADER
00450  NREC   DCW   #1B8      NUM RECS FOR LOADER
00450*
00450  BUFFER EQU   *
00720*
00730         END   0
